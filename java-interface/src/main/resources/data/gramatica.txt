
<programa>   	::= <instrucciones>

<instrucciones>  ::= <instrucción> <instrucciones> | ε

<instrucción>	::= <declaración>
              	| <asignación>
              	| <si>
              	| <mientras>
              	| <para>
              	| <imprimir>
              	| <entrada>

<declaración>	::= TIPO <identificador> ";"
              	| TIPO <identificador> "=" <expresión> ";"

<asignación> 	::= <identificador> "=" <expresión> ";"

<si>         	::= "si" "(" <condición> ")" "{" <instrucciones> "}"
              	| "si" "(" <condición> ")" "{" <instrucciones> "}" "si no" "{" <instrucciones> "}"

<mientras>   	::= "mientras" "(" <condición> ")" "{" <instrucciones> "}"

<para>       	::= "para" "(" <declaración> <condición> ";" <asignación> ")" "{" <instrucciones> "}"

<imprimir>   	::= "mostrar" "(" <expresión> ")" ";"

<entrada>    	::= <identificador> "=" "leer" "(" ")" ";"

<condición>  	::= <expresión> COMPARADOR <expresión>
              	| <condición> LÓGICO <condición>

<expresión>  	::= <término> | <expresión> OP <término>
<término>    	::= <número> | <identificador> | <texto> | "(" <expresión> ")"

<número>     	::= [0-9]+ | [0-9]+"."[0-9]+
<texto>      	::= "\"" .*? "\""
<identificador>  ::= [a-zA-Z_][a-zA-Z0-9_]*

TIPO         	::= "entero" | "decimal" | "texto" | "booleano"
OP           	::= "+" | "-" | "*" | "/"
COMPARADOR   	::= "==" | "!=" | "<" | ">" | "<=" | ">="
LÓGICO       	::= "&&" | "||"
